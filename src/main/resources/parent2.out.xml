<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:p="http://maven.apache.org/POM/4.0.0" xmlns="http://maven.apache.org/POM/4.0.0" xmlns:xslt="http://xml.apache.org/xslt" version="1.0">
    <xsl:output method="xml" xslt:indent-amount="4" indent="yes"/>
    <xsl:strip-space elements="*"/>
    <xsl:template match="@*|node()">
        <!-- This is the Identity design pattern. It can match everything and pass it thru. The more specific 
			templates have higher priority, so for their matching elements they take precedence. This allows just matching 
			things that need to modified. Less work. -->
        <xsl:copy>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template>
    <xsl:template match="tesssss">
        <xsl:value-of select="normalize-space()"/>
        <xsl:apply-templates select="@*|node()"/>
    </xsl:template>
    <xsl:template match="/p:project">
        <xsl:copy>
            <xsl:comment>This parent pom was auto generated by splitting the main pom into parent/child poms.</xsl:comment>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template>
    <xsl:template match="/p:project/p:name">
        <xsl:copy>Wonder generic woparent</xsl:copy>
        <xsl:element name="description">Generic woparent for all Wonder/WebObjects projects. Has repositories needed, dependency management, etc. Override in your project's pom when necessary items like distributionManagement. Sections from the wonder master pom are passed through to create the parent pom. Also, a wonder.version parameter is created, its value based off the wonder master pom.</xsl:element>
    </xsl:template>
    <xsl:template match="/p:project/p:artifactId/text()">generic-woparent</xsl:template>
    <xsl:template match="/p:project/p:properties">
        <xsl:copy>
            <xsl:element name="wonder.version">
                <xsl:value-of select="/p:project/p:version"/>
            </xsl:element>
            <xsl:apply-templates/>
        </xsl:copy>
    </xsl:template>
    <xsl:template match="/p:project/p:properties/p:skip.apple.frameworks">
        <xsl:copy>false</xsl:copy>
    </xsl:template>
    <xsl:template match="/p:project/p:properties/p:maven.test.skip"/>
    <xsl:template match="/p:project/p:properties/comment()"/>
    <!-- to change project.version -->
    <xsl:template match="p:dependencies/p:dependency/p:version[text()='${project.version}']">
        <xsl:message>
            <xsl:value-of select="."/>
        </xsl:message>
        <xsl:element name="{local-name()}">${wonder.version}</xsl:element>
    </xsl:template>
    <xsl:template match="p:modules"/>
</xsl:stylesheet>
