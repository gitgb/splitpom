<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:p="http://maven.apache.org/POM/4.0.0" xmlns="http://maven.apache.org/POM/4.0.0" xmlns:xslt="http://xml.apache.org/xslt" version="1.0">
    <xsl:output method="xml" xslt:indent-amount="4" indent="yes"/>
    <xsl:strip-space elements="*"/>
    <xsl:template match="@*|node()">
        <!-- This is the Identity design pattern. It can match everything and pass it thru. The more specific 
			templates below have higher priority, so for their matching elements they take precedence. This allows 
			just matching things that need to modified, while everything else passes thru . Less work. -->
        <xsl:copy>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template>
    <xsl:template match="/p:project">
        <xsl:copy>
            <xsl:comment>This child pom was auto generated by splitting pom into parent/child poms</xsl:comment>
            <xsl:element name="parent">
                <xsl:element name="groupId">wonder</xsl:element>
                <xsl:element name="artifactId">generic-woparent</xsl:element>
                <xsl:element name="version">
                    <xsl:value-of select="/p:project/p:version"/>
                </xsl:element>
                <xsl:element name="relativePath">./generic-woparent</xsl:element>
            </xsl:element>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template>
    <xsl:template match="/p:project/p:dependencyManagement"/>
    <xsl:template match="/p:project/p:build"/>
    <xsl:template match="/p:project/p:repositories"/>
    <xsl:template match="/p:project/p:pluginRepositories"/>
</xsl:stylesheet>
